#!/usr/bin/env elvish

fn list-items [a]{
  if (-is-dir $a) { put $a/* } else { put $a }
}

items = []

if (== (count $args) 0) {
  items = [./*]
} else {
  for i $args {
    if (eq $i '-') {
      each [x]{ items = [$@items (list-items $x)] }
    } else {
      items = [$@items (list-items $i)]
    }
  }
}

file = (mktemp -p /tmp elvmvXXXXX)
print > $file

for i $items {
  echo $i >> $file
}

editor = vi
if (has-env VISUAL) {
  editor = (get-env VISUAL)
} elif (has-env EDITOR) {
  editor = (get-env EDITOR)
}

fn abort [&code=1 a]{
  echo $a >&2
  rm $file
  exit $code
}

try {
  (external $editor) $file
} except {
  abort 'Editor exited with error. Aborting...'
}

i = 0
cat $file | each [l]{
  if (eq $l '') {
    rm $items[$i]
  } elif (not-eq $l $items[$i]) {
    mkdir -p (path-dir $l)
    mv $items[$i] $l
  }
  i = (+ $i 1)
}

rm $file
