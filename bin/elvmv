#!/usr/bin/env elvish

use path

fn list-items {|a|
  if (path:is-dir $a) { put $a/* } else { put $a }
}

fn get-editor {
  if (has-env VISUAL) {
    put $E:VISUAL
  } elif (has-env EDITOR) {
    put $E:EDITOR
  } else {
    put vi
  }
}

var items = []

if (== (count $args) 0) {
  set items = [./*]
} else {
  for i $args {
    if (eq $i '-') {
      each {|x| set items = [$@items (list-items $x)] }
    } else {
      set items = [$@items (list-items $i)]
    }
  }
}

var file = (mktemp -p /tmp elvmvXXXXX)
print > $file

for i $items {
  echo $i >> $file
}

fn abort {|&code=1 a|
  echo $a >&2
  rm $file
  exit $code
}

try {
  (external (get-editor)) $file
} except {
  abort 'Editor exited with error. Aborting...'
}

var i = 0
cat $file | each {|l|
  if (eq $l '') {
    rm $items[$i]
  } elif (not-eq $l $items[$i]) {
    mkdir -p (path:dir $l)
    mv $items[$i] $l
  }
  set i = (+ $i 1)
}

rm $file
