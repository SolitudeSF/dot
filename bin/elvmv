#!/usr/bin/env elvish

use os
use file
use path

fn list-items {|a|
  if (path:is-dir $a) { put $a/* } else { put $a }
}

fn get-editor {
  if (has-env VISUAL) {
    put $E:VISUAL
  } elif (has-env EDITOR) {
    put $E:EDITOR
  } else {
    put vi
  }
}

var items = []

if (== (count $args) 0) {
  set items = [./*]
} else {
  for i $args {
    if (eq $i '-') {
      each {|x| set items = (conj $items (list-items $x)) }
    } else {
      set items = (conj $items (list-items $i))
    }
  }
}

var file = (os:temp-file elvmv)
print > $file

for i $items {
  echo $i >> $file
}

try {
  (external (get-editor)) $file
} catch {
  os:remove $file
  echo 'Editor exited with error. Aborting...' >&2
  exit 1
}

var i = 0
cat $file[name] | each {|l|
  if (eq $l '') {
    os:remove-all $items[$i]
  } elif (not-eq $l $items[$i]) {
    os:mkdir-all (path:dir $l)
    os:rename $items[$i] $l
  }
  set i = (+ $i 1)
}

file:close $file
os:remove $file[name]
